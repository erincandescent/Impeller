buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.8.3'
    }
}

apply plugin: 'android'

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.3"

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }

    sourceSets {
        main {
            // We use the traditional project directory structure
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        instrumentTest.setRoot('tests')
    }

    signingConfigs {
        release
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.android.support:support-v4:19.0.1'
    compile 'oauth.signpost:signpost-core:1.2'
    compile 'com.google.guava:guava:16.0.1'
    compile 'net.sourceforge.htmlcleaner:htmlcleaner:2.7'
    compile project(':jiraconnect-android-main')
	
	
	

    // HTML parsing and generation
}

configurations {
    all*.exclude group: 'xml-apis'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}

def Properties signingConfig = new Properties()
def keyPropFile = new File('keys.properties')
if(keyPropFile.canRead()) {
    signingConfig.load(new FileInputStream(keyPropFile))
}

if (signingConfig.containsKey('STORE_FILE')
  && signingConfig.containsKey('STORE_PASSWORD')
  && signingConfig.containsKey('KEY_ALIAS')
  && signingConfig.containsKey('KEY_PASSWORD')) {
    android.signingConfigs.release.storeFile     = file(signingConfig['STORE_FILE'])
    android.signingConfigs.release.storePassword = signingConfig['STORE_PASSWORD']
    android.signingConfigs.release.keyAlias      = signingConfig['KEY_ALIAS']
    android.signingConfigs.release.keyPassword   = signingConfig['KEY_PASSWORD']
} else {
    android.buildTypes.release.signingConfig = null
    println 'No signing configuration or is incomplete'
}